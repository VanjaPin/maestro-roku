'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ Initialization
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function init()
  m.log.method("init")
  initializeView(m.top.navController)
  m.appController = ioc.getInstance("appController")
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ Private impl
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function applyDeeplinking()
  m.log.method("applyDeeplinking")
  deeplinkingData = m.modelLocator.deeplinkingData

  isGridPresent = deeplinkingData.mediaType <> "vod"

  if isGridPresent
    gridScreen = createObject("roSGNode", "GridScreen")
    gridScreen.selection = deeplinkingData.selection
    push(gridScreen)
  end if

  if deeplinkingData.action = "play"
    m.appController@.playSelection(deeplinkingData.selection)
  end if

  if isGridPresent or deeplinkingData.action = "play"
    setFocus(m.top.navController)
  else
    m.log.info("No Grid present, and no played content, so we will not enter deep linking")
    m.appController@.exitFromDeepLinking()
  end if
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ Key handling
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function onKeyPressBack() as boolean
  m.log.method("onKeyPressBack")
  pop()
  setFocus(m.top.navController)
  if m.top.navController.numberOfViews = 0
    m.appController@.exitFromDeepLinking()
  end if
  return m.top.navController.numberOfViews > 0
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ Lifecycle methods
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function onShow()
  m.log.method("onShow")
  applyDeeplinking()
end function

function onHide()
  m.log.method("onHide")
end function

function onGainedFocus(isSelfFocused)
  m.log.method("onGainedFocus ", isSelfFocused)
  if isSelfFocused
    setFocus(m.top.navController)
  end if
end function
