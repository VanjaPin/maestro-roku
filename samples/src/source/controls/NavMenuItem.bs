import "pkg:/source/roku_modules/maestro/view/BaseView.brs"

namespace controls
  @observerswaitinitialize
  @node("NavMenuItem", "BaseView")
  class NavMenuItem extends view.BaseView

    @debounce
    @observer("redraw")
    public item as types.node

    @debounce
    @observer("redraw")
    public isParentInFocus = true

    'views
    private nameLabel

    function new()
      super("NavMenu")
    end function

    protected override function initialize()
      super.initialize()
      m.createViews()
      m.redraw()
    end function

    private function createViews()
      m.nameLabel = m.createSGNode("Label", m.top, "nameLabel", {
        horizAlignment: "center"
        vertAlignment: "center"
        fontKey: "large,40"
        itemSpacings: [30]
      })
    end function


    function redraw() as void
      text = m.item.name
      if m.item.isFocused
        text = "+" + text
        m.nameLabel.color = m.isParentInFocus ? "#ffffff" : "#bbbbbb"
      else
        m.nameLabel.color = "#bbbbbb"
      end if
      m.nameLabel.text = text
    end function
  end class
end namespace