import "pkg:/source/roku_modules/log/LogMixin.brs"
import "pkg:/source/core/Tasks.bs"
import "pkg:/source/core/IOC.bs"
import "pkg:/source/view/BaseView.bs"

namespace view
  ' /**
  '  * @module BaseScreen
  '  * @description Class based counterpart to BaseView component
  '  *              used for MVC style programming
  '  */
  class BaseScreen extends view.BaseView

    'field to allow for convenient tracking of a view
    @alwaysnotify
    public state as string

    function new(name as string)
      super(name)
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ nav support
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    'Note the following methods are proxies to facilitate testing.
    'All of them will call out to the top level scope

    ' /**
    '  * @member push
    '  * @memberof module:BaseViewModel
    '  * @instance
    '  * @function
    '  * @description wrapper for push
    '  * @param {view} the view to push, if this is a string, then it is looked up on m - so you can push views by their id, as well as actual views
    '  */
    protected function push(v as types.node, animated = true as boolean)
      'bs:disable-next-line
      push(v, animated)
    end function

    protected function pop(animated = true as boolean) as types.node
      'bs:disable-next-line
      return(pop(animated))
    end function

    protected function resetNavControllerToRoot(animated = true as boolean)
      'bs:disable-next-line
      resetNavControllerToRoot(animated)
    end function

    protected function getNavController()
      'bs:disable-next-line
      return getNavController()
    end function

    protected function resetNavController(newFirstScreen = invalid as types.node, endIndex = -1 as integer, animated = false as boolean)
      'bs:disable-next-line
      resetNavController(newFirstScreen, endIndex, animated)
    end function

    protected function dismiss(state as string, animated = true as boolean)
      if m.getTopField("navController") <> invalid
        m.pop(animated)
      end if

      if state <> invalid
        m.state = state
        'This cannot be inferred because this is an abstract node class
        'bs:disable-next-line
        m.top.state = state
      end if
    end function

    function replaceNavControllerScreen(targetScreen as types.node, animated = false as boolean)
      'bs:disable-next-line
      replaceNavControllerScreen(targetScreen, animated)
    end function
  end class
end namespace