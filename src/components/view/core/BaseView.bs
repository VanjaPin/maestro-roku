import "pkg:/source/roku_modules/log/LogMixin.brs"
import "pkg:/source/core/Utils.bs"
import "pkg:/components/view/mixin/FocusMixin.bs"
import "pkg:/components/view/mixin/KeyMixin.bs"
import "pkg:/source/core/Utils.bs"
import "pkg:/source/view/ViewUtils.bs"
import "pkg:/source/core/IOC.bs"
import "pkg:/source/core/Types-Polyfill.bs"

' /**
'  * @module BaseView
'  * @description BaseView class for all controls or screens
'  */
function init() as void
  m.log = new log.Logger(getViewName())
  ioc.initialize()
  m.top.observeFieldScoped("isFocused", "onIsFocusedChange")
  m.top.observeFieldScoped("isChildFocused", "onIsChildFocusedChange")

  keyPressMixinInit()

  'TODO - guard these observers to not include in prod builds
  m.top.observeFieldScoped("__debugM", "onDebugMChange")

  m.wasShown = false
end function

function getViewName()
  return m.top.subType()
end function

function onDebugMChange()
  setOrAddNodeField(m.top, "__m", m)
  setOrAddNodeField(m.top, "__vm", m.vm)
end function

' /**
'  * @member initializeView
'  * @memberof module:BaseView
'  * @instance
'  * @function
'  * @description initializes the passed in View
'  * @param {BaseView} the view to initialize
'  */
function initializeView(v as types.node, args = invalid as types.assocarray) as void
  if v <> invalid
    v@._initialize(args)
  else
    m.log.error("view is invalid!")
  end if
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'** VISIBILITY
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function onVisibleChange()
  'TODO - does the nav controller handle this in future?
  m.log.info(m.top.id, "onVisibleChange visible ", m.top.visible)
  if m.top.visible
    _onShow(invalid)
  else
    _onHide(invalid)
  end if
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ Lifecycle methods
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function _onShow(args as types.assocarray) as void
  ' ? ">> base VIEW _OnShow " ; m.top.id ; " isShown " ; m.top.isShown
  oldIsShowing = m.top.isShown
  if not m.top.isInitialized
    return
  end if
  m.top.isShown = true

  if not m.wasShown
    'bs:disable-next-line
    if isFunction(onFirstShow)
      'bs:disable-next-line
      onFirstShow()
    end if
    m.wasShown = true

    if m.vm <> invalid and m.vm.onFirstShow <> invalid
      m.vm.onFirstShow()
    else if m.onFirstShow <> invalid
      m.onFirstShow()
    end if

  end if

  if oldIsShowing <> m.top.isShown
    'bs:disable-next-line
    if isFunction(onShow)
      'bs:disable-next-line
      onShow()
    end if

    if m.vm <> invalid and m.vm.onShow <> invalid
      m.vm.onShow()
    else if m.onShow <> invalid
      m.onShow()
    end if

  end if
end function

function _onHide(args as types.assocarray)
  if m.wasShown
    m.top.isShown = false
    'bs:disable-next-line
    if isFunction(onHide)
      'bs:disable-next-line
      onHide()
    end if

    if m.vm <> invalid and m.vm.onHide <> invalid
      m.vm.onHide()
    end if
    if m.onHide <> invalid
      m.onHide()
    end if

  else
    m.log.warn("onHide called before show: ignoring")
  end if
end function

function _initialize(args = invalid as types.assocarray)
  if not m.top.isInitialized
    m.top.isInitialized = true
    initialize(args)
    if m._initialize <> invalid
      m._initialize()
    end if
    'bs:disable-next-line
    if m.pendingCallbacks <> invalid and m.pendingCallbacks.count() > 0 and isFunction(executePendingCallbacks)
      'bs:disable-next-line
      executePendingCallbacks()
    end if
    'bs:disable-next-line
    if isFunction(m_createVM)
      'bs:disable-next-line
      m_createVM()
    end if
    m.top.observeFieldScoped("visible", "onVisibleChange")
    if m.top.visible and not m.top.isShown
      _onShow(invalid)
    end if
  else
    m.log.warn("View was already initialized. Ignoring subsequent call ", m.top)
  end if
end function

function onAddedToAggregateView(v as types.node)
  if m.vm <> invalid and m.vm.onAddedToAggregateView <> invalid
    m.vm.onAddedToAggregateView(v)
  else if m.onAddedToAggregateView <> invalid
    m.onAddedToAggregateView(v)
  end if
end function

function onRemovedFromAggregateView(v as types.node)
  if m.vm <> invalid and m.vm.onRemovedFromAggregateView <> invalid
    m.vm.onRemovedFromAggregateView(v)
  else if m.onRemovedFromAggregateView <> invalid
    m.onRemovedFromAggregateView(v)
  end if
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ focus
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function onIsFocusedChange()
  if m.global.focusManager.isLogging
    m.log.verbose("m.top.isFocused", m.top.isFocused, dv(m.top))
  end if

  if m.top.isFocused
    onGainedFocus(true)
  else
    onLostFocus()
  end if
end function

function onIsChildFocusedChange()
  if m.global.focusManager.isLogging
    m.log.verbose("m.top.isChildFocused", m.top.isChildFocused, dv(m.top))
  end if

  if m.top.isChildFocused
    onGainedFocus(false)
  else
    'we don't notify about this
    onChildLostFocus()
  end if
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ abstract focus methods
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

' /**
'  * @member onGainedFocus
'  * @memberof module:FocusMixin
'  * @instance
'  * @function
'  * @description invoked when a control, or a parent of a focused control, gains focus
'  * @param {boolean} isSelfFocused - if true, then this is the control that is focused, if false, then it is one of the control's parents
'  */
function onGainedFocus(isSelfFocused as boolean)
  if m.global.focusManager.isLogging
    m.log.info("onGainedFocus", m.top.subType(), isSelfFocused)
  end if

  if isSelfFocused
    if m.vm <> invalid
      if m.vm.onGainedFocus <> invalid
        m.vm.onGainedFocus(isSelfFocused)
      end if

      'bs:disable-next-line
      if isFunction(mx_onFocusIdChange)
        'bs:disable-next-line
        mx_onFocusIdChange(m.vm.focusId)
      end if
    else if m.onGainedFocus <> invalid
      m.onGainedFocus(isSelfFocused)
    end if

  end if
end function

' /**
'  * @member onGainedFocus
'  * @memberof module:FocusMixin
'  * @instance
'  * @function
'  * @description invoked when a control, loses focus
'  */
function onLostFocus()
  if m.global.focusManager.isLogging
    m.log.info("lost focus:", m.top.subType(), ".", m.top.id)
  end if
  if m.vm <> invalid and m.vm.onLostFocus <> invalid
    m.vm.onLostFocus()
  else if m.onLostFocus <> invalid
    m.onLostFocus()
  end if
end function

function onChildLostFocus()
  if m.global.focusManager.isLogging
    m.log.info("lost focus:", m.top.subType(), ".", m.top.id)
  end if
  if m.vm <> invalid and m.vm.onChildLostFocus <> invalid
    m.vm.onChildLostFocus()
  else if m.onChildLostFocus <> invalid
    m.onChildLostFocus()
  end if
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ utils
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function callVMFunc(funcName as string, p1 = "#M_NONE#" as dynamic, p2 = "#M_NONE#" as dynamic, p3 = "#M_NONE#" as dynamic)
  if m.vm <> invalid
    return callAAFunc(m.vm, funcName, p1, p2, p3)
  else
    return m.callAAFunc(m, funcName, p1, p2, p3)
  end if
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ abstract lifecycle methods
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function initialize(args as types.assocarray)
end function
